plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdk 31

    defaultConfig {
        configurations.all {
            resolutionStrategy { force 'androidx.core:core-ktx:1.6.0' }
        }
        applicationId "com.example.marvelapp"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug{
            buildConfigField("String", "marvel_api_private", localProperties["marvel.api.priv_key"])
            buildConfigField("String", "marvel_api_public", localProperties["marvel.api.pub_key"])

        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "environment"
    productFlavors{
        development {
            dimension "environment"
            applicationIdSuffix = ".dev"
            versionNameSuffix '-dev'
            ext {
                baseURL = "https://gateway.marvel.com:443/v1/public/"
            }
        }

        errorMock {
            dimension "environment"
            applicationIdSuffix = ".error"
            versionNameSuffix '-error'
            ext {
                baseURL = "https://4cff91d8-d70b-4a72-ab9e-d33eeb905fb6.mock.pstmn.io"
            }
        }

        successMock {
            dimension "environment"
            applicationIdSuffix = ".success"
            versionNameSuffix '-success'
            ext {
                baseURL = "https://b1a25709-784f-44c5-848b-63b6272a9cbc.mock.pstmn.io"
            }
        }


        applicationVariants.all { variant ->

            def dimensions = variant.productFlavors
            def environment = dimensions[0]

            def BASE_URL = environment.ext.baseURL
            variant.buildConfigField "String", "base_URL", "\"$BASE_URL\""

        }
    }

}

dependencies {

    def versionVM = "2.4.0"

    //ML Kit Barcode Scanning
    implementation 'com.google.mlkit:barcode-scanning:17.0.1'

    //CameraX Dependencies
    implementation "androidx.camera:camera-core:1.1.0-alpha12"
    implementation "androidx.camera:camera-camera2:1.1.0-alpha12"
    implementation "androidx.camera:camera-lifecycle:1.1.0-alpha12"
    implementation "androidx.camera:camera-view:1.0.0-alpha32"
    implementation "androidx.camera:camera-extensions:1.0.0-alpha32"
    implementation 'com.google.android.material:material:1.6.0-alpha01'

    //Library to get URL Meta Data
    implementation 'org.jsoup:jsoup:1.13.1'

    //lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versionVM"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versionVM"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2-native-mt"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2-native-mt'

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation('com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2')
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:17.0.0'

    implementation 'androidmads.library.qrgenearator:QRGenearator:1.0.3'
}